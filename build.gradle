plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id "net.covers1624.signing" version '1.1.4' apply false
    id 'net.neoforged.gradle' version '[6.0.18,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
    withJavadocJar()
}

group = 'codechicken'
archivesBaseName = 'CodeChickenLib'

sourceSets.main.resources.srcDirs += "src/main/generated"

file('build.properties').withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

config.mod_version = "${config.mod_version}." + (System.getenv("BUILD_NUMBER") ?: "1")
version = "${config.mc_version}-${config.mod_version}"
println "Starting build of ${archivesBaseName}, Version: ${config.mod_version}"
println "Using Forge: ${config.forge_version}, for Minecraft: ${config.mc_version}"

minecraft {
    mappings channel: 'official', version: config.mc_version
    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")
    runs {
        client {
            workingDirectory file('run')
            mods { codechickenlib { source sourceSets.main } }
        }
        server {
            workingDirectory file('run')
            mods { codechickenlib { source sourceSets.main } }
        }
        data {
            workingDirectory file('run')
            args '--mod', 'codechickenlib', '--all', '--output', file("src/main/generated"), '--existing', file("src/main/resources")
            mods { codechickenlib { source sourceSets.main } }
        }
    }
}

mixin {
    add(sourceSets.main, "mixins.codechickenlib.refmap.json")
}

processResources {
    inputs.property 'mod_version', config.mod_version
    inputs.property 'mc_version', config.mc_version

    filesMatching('META-INF/mods.toml') {
        expand 'file': ['jarVersion': config.mod_version],
                'mc_version': config.mc_version,
                'forge_version': config.forge_version,
                'lang_version': config.forge_version.split('\\.')[0]
    }
}

configurations {
    shadow
    implementation.extendsFrom shadow
    annotationProcessor.extendsFrom minecraft
}

repositories {
    mavenLocal()
    maven { url "https://maven.covers1624.net/" }
}

dependencies {
    minecraft "net.neoforged:forge:${config.mc_version}-${config.forge_version}"

    shadow 'net.covers1624:Quack:0.4.7.72'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

reobf {
    shadowJar {}
    jar { enabled = false } // Disable jar.
}

def commonManifest = {
    attributes 'Specification-Title': archivesBaseName
    attributes 'Specification-Vendor': 'covers1624'
    attributes 'Specification-Version': "1"
    attributes 'Implementation-Title': archivesBaseName
    attributes 'Implementation-Vendor': 'covers1624'
    attributes 'Implementation-Version': config.mod_version
    attributes 'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    attributes 'MixinConfigs': 'mixins.codechickenlib.json'
}

jar.enabled = false
shadowJar {
    finalizedBy 'reobfShadowJar'
    archiveClassifier = 'universal'
    manifest commonManifest

    configurations = [project.configurations.shadow]
    from file("LICENSE")
}

def jarArchive = shadowJar
if (System.getenv("KEY_STORE") || project.hasProperty('keyStore')) {
    jarArchive = tasks.register("signShadowJar", net.covers1624.jarsign.jar.SignJarTask) {
        dependsOn("shadowJar")
        dependsOn("reobfShadowJar")

        input = shadowJar.archiveFile.get().asFile
        output = shadowJar.archiveFile.get().asFile
        if (System.getenv("KEY_STORE")) {
            keyStore = System.getenv("KEY_STORE")
            file(System.getenv("KEY_STORE_PROPS")).withReader {
                def props = new Properties()
                props.load(it)
                alias = props['alias']
                storePass = props['storePass']
                keyPass = props['keyPass']
            }
        } else {
            keyStore = project.getProperty('keyStore')
            alias = project.getProperty('keyStoreAlias')
            storePass = project.getProperty('keyStorePass')
            keyPass = project.getProperty('keyStoreKeyPass')
        }
    }
}

javadoc {
    options.addBooleanOption("Xdoclint:none", true)
}

publishing {
    repositories {
        if (System.getenv('MAVEN_PASS')) {
            maven {
                url "https://nexus.covers1624.net/repository/maven-releases/"
                credentials {
                    username 'covers1624'
                    password System.getenv('MAVEN_PASS')
                }
            }
        }
    }
    publications {
        CodeChickenLib(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact jarArchive
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = archivesBaseName
                description = archivesBaseName
                url = "https://github.com/TheCBProject/${archivesBaseName}"
                scm {
                    url = "https://github.com/TheCBProject/${archivesBaseName}"
                    connection = "scm:git:git@github.com:TheCBProject/${archivesBaseName}.git"
                }

                issueManagement {
                    system = 'github'
                    url = "https://github.com/TheCBProject/${archivesBaseName}/issues"
                }
                licenses {
                    license {
                        name = "GNU Lesser Public License (GPL), Version 2.1"
                        url = "https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt"
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'covers1624'
                        name = 'covers1624'
                    }
                }
                developers {
                    developer {
                        id = 'chicken-bones'
                        name = 'chicken-bones'
                    }
                }
            }
        }
    }
}

signing {
    if (System.getenv('GPG_PRIVATE_KEY')) {
        useInMemoryPgpKeys(
                new File(System.getenv('GPG_PRIVATE_KEY')).text,
                System.getenv('GPG_PRIVATE_KEY_PASS')
        )
    } else if (project.hasProperty('gpgPrivateKey')) {
        useInMemoryPgpKeys(
                new File(project.getProperty('gpgPrivateKey')).text,
                project.getProperty('gpgPrivateKeyPass')
        )
    } else {
        return
    }
    sign publishing.publications.CodeChickenLib
}

/**
 * Polls the 'compile' configuration for a dependency with the given module name
 * and resolves, and returns its version. E.g: '1.2.+' will resolve to '1.2.3.4'
 *
 * @param module The module to search for.
 * @param chop If the string should be chopped on the last '-' in its string.
 * @param configuration The name of the configuration to search.
 * @param errorMissing If an error should be thrown if it can't be found.
 * @return The version string, '0' if 'errorMissing' is false and nothing was found.
 */
def resolve(module, chop = true, configuration = 'compile', errorMissing = true) {
    //Copy and lenient resolve the configuration, Forge cant be resolved at this time so lenient is required.
    def filtered = configurations.getByName(configuration).copy().incoming.artifactView({ it.lenient = true }).artifacts
            .findAll { it.id.componentIdentifier.module == module }
            .collect { it.id.componentIdentifier.version }
    if (filtered.size() > 1) {
        println "WARNING: Found ${filtered.size()} Dependencies with ModuleName '${module}' in configuration '${configuration.name}'"
    }
    if (errorMissing && filtered.isEmpty()) {
        throw new RuntimeException("Failed resolve dependency version for '${module}'")
    }
    if (filtered.isEmpty()) return "0"
    def version = filtered.first() as String
    if (chop) {
        def idx = version.lastIndexOf('-')
        return version.substring(idx + 1)
    }
    return version
}

